apply plugin: 'com.android.application'

// read version from package.json
def packageJson = new groovy.json.JsonSlurper().parse(file("../../package.json"))
def appVersion = packageJson.version
// convert semver to versionCode: 0.5.6 -> 506, 1.2.3 -> 10203
def versionParts = appVersion.split('\\.')
def appVersionCode = (versionParts[0].toInteger() * 10000) + (versionParts[1].toInteger() * 100) + versionParts[2].toInteger()

android {
    namespace "me.faeller.zulipnotifs"
    compileSdk rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "me.faeller.zulipnotifs"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode appVersionCode
        versionName appVersion
        // expose version to java via BuildConfig
        buildConfigField "String", "APP_VERSION", "\"${appVersion}\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }
    signingConfigs {
        if (System.getenv("KEYSTORE_PASSWORD")) {
            release {
                storeFile file("release.keystore")
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // only sign if keystore is configured (not on F-Droid)
            if (System.getenv("KEYSTORE_PASSWORD")) {
                signingConfig signingConfigs.release
            }
        }
    }
    buildFeatures {
        buildConfig true
    }
}

repositories {
    flatDir{
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation "androidx.work:work-runtime:2.9.0"
    implementation project(':capacitor-android')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
}

apply from: 'capacitor.build.gradle'

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
